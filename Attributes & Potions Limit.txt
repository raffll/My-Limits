begin raffll_limits

short maxValue
short oldValue
short speedOffset
short potionTime
short limitStrength
short limitIntelligence
short limitWillpower
short limitAgility
short limitSpeed
short limitEndurance
short limitPersonality
short limitLuck
short limitAttribute
short limitPotion
short limit
short active
short maxFatigue
short drinkCount
short drinkOverdose
short drinkDeath
short playerEndurance
short playerWillpower
short playerLevel
float timer
short maxCount
short oldCount
short message

set speedOffset to 0
set potionTime to 20
set drinkOverdose to 90
set drinkDeath to 100

if ( active == 0 )
	set playerEndurance to ( player->GetEndurance )
	if ( playerEndurance > 100 )
		set playerEndurance to 100
	endif

	set playerWillpower to ( player->GetWillpower )
	if ( playerWillpower > 100 )
		set playerWillpower to 100
	endif

	set playerLevel to ( player->GetLevel )

	set maxValue to ( 101 + playerEndurance + playerWillpower + playerLevel )
	
	if ( maxValue != oldValue )
		set message to 1
		set oldValue to maxValue
	endif

	set maxCount to ( player->GetAlchemy )
	set maxCount to ( ( maxCount / 20 ) + 3 )

	if ( maxCount < 3 )
		set maxCount to 3
	endif

	if ( maxCount > 8 )
		set maxCount to 8
	endif

	if ( maxCount != oldCount )
		set message to 1
		set oldCount to maxCount
	endif
	
	if ( message == 1)
		set maxValue to ( maxValue - 1 )
		MessageBox "You can drink up to %G potions.\nYour attributes cap is %G.", maxCount, maxValue
		set maxValue to ( maxValue + 1 )
		set message to 0
	endif
endif

if ( ( player->GetStrength ) < maxValue )
	set limitStrength to 0
else
	set limitStrength to 1
endif

if ( ( player->GetIntelligence ) < maxValue )
	set limitIntelligence to 0
else
	set limitIntelligence to 1
endif

if ( ( player->GetWillpower ) < maxValue )
	set limitWillpower to 0
else
	set limitWillpower to 1
endif

if ( ( player->GetAgility ) < maxValue )
	set limitAgility to 0
else
	set limitAgility to 1
endif

if ( ( player->GetSpeed ) < ( maxValue + speedOffset ) )
	set limitSpeed to 0
else
	set limitSpeed to 1
endif

if ( ( player->GetEndurance ) < maxValue )
	set limitEndurance to 0
else
	set limitEndurance to 1
endif

if ( ( player->GetPersonality ) < maxValue )
	set limitPersonality to 0
else
	set limitPersonality to 1
endif

if ( ( player->GetLuck ) < maxValue )
	set limitLuck to 0
else
	set limitLuck to 1
endif

if ( limitStrength == 1 )
	set limitAttribute to 1
elseif ( limitIntelligence == 1 )
	set limitAttribute to 1
elseif ( limitWillpower == 1 )
	set limitAttribute to 1
elseif ( limitAgility == 1 )
	set limitAttribute to 1
elseif ( limitSpeed == 1 )
	set limitAttribute to 1
elseif ( limitEndurance == 1 )
	set limitAttribute to 1
elseif ( limitPersonality == 1 )
	set limitAttribute to 1
elseif ( limitLuck == 1 )
	set limitAttribute to 1
else
	set limitAttribute to 0
endif

if ( active == 0 )
	if ( limitAttribute == 1 )
		MessageBox "You reach your body's physical limit!"
	endif
endif

;;;

if ( player->GetSoundPlaying "drink" == 1 )
	player->StopSound "drink"
	if ( timer < potionTime )
		
		if ( drinkCount < drinkOverdose )
			if ( maxCount < drinkCount )
				set drinkCount to drinkOverdose
			endif
		endif
	
		if ( drinkCount == drinkDeath )
			MessageBox "You have died from the potion overdose!"
			player->SetHealth 0
			set timer to 0
			MenuTest
			
		elseif ( drinkCount == drinkOverdose )
			MessageBox "You have overdosed the potions!"
			player->Equip "raffll_tox90"
			player->StopSound "drink"
			set limitPotion to 1
			set timer to 0
			set drinkCount to drinkDeath
			
		elseif ( drinkCount == 7 )
			player->Equip "raffll_tox8"
			player->StopSound "drink"
			set timer to 0
			if ( maxCount == 8 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 8
			endif
			
		elseif ( drinkCount == 6 )
			player->Equip "raffll_tox7"
			player->StopSound "drink"
			set timer to 0
			if ( maxCount == 7 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 7
			endif
			
		elseif ( drinkCount == 5 )
			player->Equip "raffll_tox6"
			player->StopSound "drink"
			set timer to 0
			if ( maxCount == 6 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 6
			endif
			
		elseif ( drinkCount == 4 )
			player->Equip "raffll_tox5"
			player->StopSound "drink"
			set timer to 0
			if ( maxCount == 5 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 5
			endif
			
		elseif ( drinkCount == 3 )
			player->Equip "raffll_tox4"
			player->StopSound "drink"
			set timer to 0
			if ( maxCount == 4 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 4
			endif
			
		elseif ( drinkCount == 2 )
			player->Equip "raffll_tox3"
			player->StopSound "drink"
			set timer to 0
			if ( maxCount == 3 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 3
			endif
			
		elseif ( drinkCount == 1 )
			player->Equip "raffll_tox2"
			player->StopSound "drink"
			set timer to 0
			set drinkCount to 2
			
		else
			player->Equip "raffll_tox1"
			player->StopSound "drink"
			set timer to 0
			set drinkCount to 1
		endif
	endif
endif

if ( drinkCount > 0 )
	if ( MenuMode == 0 )
		set timer to ( timer + GetSecondsPassed )
		if ( timer >= potionTime )		
			MessageBox "You have recovered from the potion toxic effect!"
			set limitPotion to 0
			set timer to 0
			set drinkCount to 0
		endif
	endif
endif

;;;

if ( limitAttribute == 1 )
	set limit to 1
elseif ( limitPotion == 1 )
	set limit to 1
else
	set limit to 0
endif

if ( active == 0 )
	if ( limit == 0 )
		return
	endif
endif

if ( active == 1 )
	if ( limit == 1 )
		player->SetFatigue 0
		return
	endif
endif

if ( active == 0 )
	if ( limit == 1 )
		player->AddSpell "raffll_limit"
		player->SetFatigue 0
		set active to 1
		MenuTest
		return
	endif
endif

if ( active == 1 )
	if ( limit == 0 )
		MessageBox "You have fully recovered!"
		player->RemoveSpell "raffll_limit"
		set maxFatigue to ( player->GetStrength + player->GetWillpower + player->GetAgility + player->GetEndurance )
		player->SetFatigue maxFatigue
		set maxFatigue to ( maxFatigue * -1 )
		player->ModCurrentFatigue maxFatigue
		set active to 0
		return
	endif
endif

end