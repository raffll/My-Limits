begin raffll_limits

short init

short maxCount
short oldCount

short potionTime

short limitPotion
short limit
;short active

short drinkCount
short drinkOverdose
short drinkDeath

short maxFatigue
float timer

float currentHour
float drinkHour
float deltaHour

float countdown

if ( CharGenState != -1 )
	return
endif

if ( init == 0 )
	set potionTime to 20
	set drinkOverdose to 90
	set drinkDeath to 100
	set init to 1
endif

if ( drinkMsg == 1 )
	set countdown to ( potionTime - timer )
	MessageBox "You can't drink any more potions for %.0fs.", countdown
	set drinkMsg to 0;
endif

if ( drinkMsg2 == 1 )
	MessageBox "You can't drink potions right now."
	set drinkMsg2 to 0;
endif

if ( apparatusMsg == 1 )
	MessageBox "You can't create potions right now."
	set apparatusMsg to 0;
endif

if ( active == 0 )
	set maxCount to ( player->GetAlchemy )
	set maxCount to ( ( maxCount / 20 ) + 3 )

	if ( maxCount < 3 )
		set maxCount to 3
	endif

	if ( maxCount > 8 )
		set maxCount to 8
	endif

	if ( maxCount != oldCount )
		MessageBox "You can drink up to %G potions.", maxCount
		set oldCount to maxCount
	endif
endif

;;;

if ( player->GetSoundPlaying "drink" == 1 )
	player->StopSound "drink"
	if ( timer < potionTime )
		
		set drinkHour to GameHour
		
		if ( drinkCount < drinkOverdose )
			if ( maxCount < drinkCount )
				set drinkCount to drinkOverdose
			endif
		endif
	
		if ( drinkCount == drinkDeath )
			MessageBox "You have died from the potion overdose!"
			player->SetHealth 0
			set timer to 0
			MenuTest
			
		elseif ( drinkCount == drinkOverdose )
			MessageBox "You have overdosed potions!"
			set limitPotion to 1
			set timer to 0
			set drinkCount to drinkDeath
			
		elseif ( drinkCount == 7 )
			set timer to 0
			if ( maxCount == 8 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 8
			endif
			
		elseif ( drinkCount == 6 )
			set timer to 0
			if ( maxCount == 7 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 7
			endif
			
		elseif ( drinkCount == 5 )
			set timer to 0
			if ( maxCount == 6 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 6
			endif
			
		elseif ( drinkCount == 4 )
			set timer to 0
			if ( maxCount == 5 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 5
			endif
			
		elseif ( drinkCount == 3 )
			set timer to 0
			if ( maxCount == 4 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 4
			endif
			
		elseif ( drinkCount == 2 )
			set timer to 0
			if ( maxCount == 3 )
				set drinkCount to drinkOverdose
			else
				set drinkCount to 3
			endif
			
		elseif ( drinkCount == 1 )
			set timer to 0
			set drinkCount to 2
			
		else
			set timer to 0
			set drinkCount to 1
		endif
		
		if ( drinkCount == drinkOverdose )
			MessageBox "You have reached the limit of potions!"
		endif
		
	endif
endif

if ( drinkCount > 0 )
	if ( MenuMode == 0 )
		set timer to ( timer + GetSecondsPassed )
	endif
	
	set currentHour to GameHour
	;MessageBox "%.2f, %.2f, %.2f, %.2f", timer, drinkHour, currentHour, deltaHour
	set deltaHour to ( currentHour - drinkHour )
	if ( deltaHour < 0 )
		set deltaHour to ( deltaHour * -1 )
	endif
	if ( deltaHour > 1 )
		set timer to ( potionTime + 1 )
	endif
	
	if ( timer >= potionTime )		
		MessageBox "You have recovered from the potion toxic effect!"
		set limitPotion to 0
		set timer to 0
		set drinkCount to 0
	endif
endif

;;;

if ( limitPotion == 1 )
	set limit to 1
else
	set limit to 0
endif

if ( active == 0 )
	if ( limit == 0 )
		return
	endif
endif

if ( active == 1 )
	if ( limit == 1 )
		player->SetFatigue 0
		return
	endif
endif

if ( active == 0 )
	if ( limit == 1 )
		player->SetFatigue 0
		set active to 1
		MenuTest
		return
	endif
endif

if ( active == 1 )
	if ( limit == 0 )
		MessageBox "You have fully recovered!"
		set maxFatigue to ( player->GetStrength + player->GetWillpower + player->GetAgility + player->GetEndurance )
		player->SetFatigue maxFatigue
		set maxFatigue to ( maxFatigue * -1 )
		player->ModCurrentFatigue maxFatigue
		set active to 0
		return
	endif
endif

end